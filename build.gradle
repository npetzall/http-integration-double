plugins {
    id "org.sonarqube" version "1.1"
    id 'me.champeau.gradle.jmh' version '0.2.0'
}

group 'npetzall.hid'
version '0.0.1'

apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'jdepend'
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'
apply plugin: 'me.champeau.gradle.jmh'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'org.testng:testng:6.9.9'
    testCompile 'org.assertj:assertj-core:2.1.0'
}

test {
    useTestNG() {
        //parallel = 'methods'
        //threadCount = Runtime.runtime.availableProcessors()
    }
    testLogging.showStandardStreams = true
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}

findbugs {
    toolVersion = "3.0.1"
    ignoreFailures = true
    effort = "max"
    reportLevel = "low"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

task jdependMainReport(dependsOn: jdependMain, group: 'verification', description: 'Generates a HTML report after executing jdepend checks.') << {
    ant.xslt(
            in: "$reporting.baseDir/jdepend/main.xml",
            style: "$rootDir/conf/jdepend/jdepend-report.xslt",
            out:"$reporting.baseDir/jdepend/main.html")
}

pmd {
    toolVersion = "5.0.4"
    sourceSets = [sourceSets.main]
    ignoreFailures = true
    ruleSets = []
    ruleSetFiles = files("$rootDir/conf/pmd/pmd-rulesets.xml")
}

jacoco {
    toolVersion = "0.7.5.201505241946"
}

test.finalizedBy jacocoTestReport

check {
    dependsOn += ['jdependMainReport']
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jmh {
    jmhVersion = '1.9.3'
    failOnError = true
    benchmarkMode = 'avgt'
    profilers = ['stack']
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}